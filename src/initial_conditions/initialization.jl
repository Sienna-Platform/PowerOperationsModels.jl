function get_initial_conditions_template(
    model::IOM.AbstractOptimizationModel,
    number_of_steps::Int,
)
    # This is done to avoid passing the duals but also not re-allocating the PTDF when it
    # exists

    network_model = NetworkModel(
        get_network_formulation(model.template);
        use_slacks = get_use_slacks(get_network_model(model.template)),
        PTDF_matrix = get_PTDF_matrix(get_network_model(model.template)),
        # Carry the MODF matrix forward so security-constrained branch models
        # (which read the registered contingencies off the network model's MODF)
        # can build inside the initial-conditions sub-model. Without this the IC
        # build hits `get_registered_contingencies(nothing)`.
        MODF_matrix = get_MODF_matrix(get_network_model(model.template)),
        reduce_radial_branches = get_reduce_radial_branches(
            get_network_model(model.template),
        ),
    )
    set_hvdc_network_model!(
        network_model,
        deepcopy(get_hvdc_network_model(model.template)),
    )
    network_model.network_reduction =
        deepcopy(get_network_reduction(get_network_model(model.template)))
    network_model.subnetworks = get_subnetworks(get_network_model(model.template))
    # Initialization builds a fresh, empty EvaluationContainer: no power-flow (or other)
    # evaluations are run during initialization.
    network_model.evaluations = IOM.EvaluationContainer()
    bus_area_map = get_bus_area_map(get_network_model(model.template))

    if !isempty(bus_area_map)
        network_model.bus_area_map = get_bus_area_map(get_network_model(model.template))
    end
    network_model.modeled_branch_types =
        get_network_model(model.template).modeled_branch_types
    ic_template = PowerOperationsProblemTemplate(network_model)
    # Do not copy events here for initialization
    for device_model in values(get_device_models(model.template))
        base_model = get_initial_conditions_device_model(model, device_model)
        base_model.use_slacks = device_model.use_slacks
        base_model.time_series_names = device_model.time_series_names
        base_model.attributes = device_model.attributes
        set_device_model!(ic_template, base_model)
    end
    for device_model in values(get_branch_models(model.template))
        base_model = get_initial_conditions_device_model(model, device_model)
        base_model.use_slacks = device_model.use_slacks
        base_model.time_series_names = device_model.time_series_names
        base_model.attributes = device_model.attributes
        set_device_model!(ic_template, base_model)
    end

    for service_model in values(get_service_models(model.template))
        base_model = get_initial_conditions_service_model(model, service_model)
        base_model.service_name = service_model.service_name
        base_model.contributing_devices_map = service_model.contributing_devices_map
        base_model.use_slacks = service_model.use_slacks
        base_model.time_series_names = service_model.time_series_names
        base_model.attributes = service_model.attributes
        set_service_model!(ic_template, get_service_name(service_model), base_model)
    end
    _reset_reduced_branch_tracker!(network_model, number_of_steps)
    if !isempty(get_service_models(model.template))
        _add_services_to_device_model!(ic_template)
    end
    return ic_template
end

function build_initial_conditions_model!(
    model::T,
) where {T <: IOM.AbstractOptimizationModel}
    internal = get_internal(model)
    set_initial_conditions_model_container!(
        internal,
        deepcopy(get_optimization_container(model)),
    )
    ic_container = get_initial_conditions_model_container(internal)
    ic_settings = deepcopy(get_settings(ic_container))
    main_problem_horizon = get_horizon(ic_settings)
    # TODO: add an interface to allow user to configure initial_conditions problem
    ic_container.JuMPmodel = IOM.make_empty_jump_model_with_settings(ic_settings)
    resolution = get_resolution(ic_settings)
    init_horizon = INITIALIZATION_PROBLEM_HORIZON_COUNT * resolution
    number_of_steps = min(init_horizon, main_problem_horizon)
    template = get_initial_conditions_template(model, number_of_steps ÷ resolution)
    ic_container.settings = ic_settings
    ic_container.built_for_recurrent_solves = false
    set_horizon!(ic_settings, number_of_steps)
    init_optimization_container!(
        get_initial_conditions_model_container(internal),
        get_network_model(get_template(model)),
        get_system(model),
    )
    JuMP.set_string_names_on_creation(
        get_jump_model(get_initial_conditions_model_container(internal)),
        false,
    )
    TimerOutputs.disable_timer!(BUILD_PROBLEMS_TIMER)

    build_problem!(
        model.internal.initial_conditions_model_container,
        template,
        get_system(model),
    )
    TimerOutputs.enable_timer!(BUILD_PROBLEMS_TIMER)
    return
end

function build_initial_conditions!(model::IOM.AbstractOptimizationModel)
    @assert get_initial_conditions_model_container(get_internal(model)) ===
            nothing
    requires_init = false
    for (device_type, device_model) in get_device_models(get_template(model))
        requires_init = requires_initialization(get_formulation(device_model)())
        if requires_init
            @debug "initial_conditions required for $device_type" _group =
                LOG_GROUP_BUILD_INITIAL_CONDITIONS
            build_initial_conditions_model!(model)
            break
        end
    end
    if !requires_init
        @info "No initial conditions in the model"
    end
    return
end
