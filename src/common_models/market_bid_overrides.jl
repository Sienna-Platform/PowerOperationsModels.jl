#################################################################################
# Device-Specific Overloads for Market Bid / Import-Export Cost
#
# These extend the generic (device-agnostic) infrastructure in IOM's
# objective_function/market_bid.jl with overloads that dispatch on
# POM-specific device types and formulation types.
#################################################################################

#################################################################################
# Section 1: Device-specific cost detection predicates
#################################################################################

_has_market_bid_cost(::PSY.RenewableNonDispatch) = false
_has_market_bid_cost(::PSY.PowerLoad) = false
_has_market_bid_cost(device::PSY.ControllableLoad) =
    PSY.get_operation_cost(device) isa IOM.MBC_TYPES

#################################################################################
# Section 1b: Generic MarketBidCost OnVariable proportional cost
#
# Shared between thermals, hydros, and interruptible loads. The OnVariable cost
# for MBC is the offer curve's `initial_input` (cost at minimum generation). The
# only per-device variation is whether that comes from the incremental side
# (generators) or the decremental side (controllable loads). Direction is set
# by the `_onvar_offer_direction` trait.
#################################################################################

_onvar_offer_direction(::PSY.Generator) = IncrementalOffer()
_onvar_offer_direction(::PSY.ControllableLoad) = DecrementalOffer()

_cost_at_min_param(::IncrementalOffer) = IncrementalCostAtMinParameter()
_cost_at_min_param(::DecrementalOffer) = DecrementalCostAtMinParameter()

# Static MarketBidCost: read initial_input directly from the offer curve.
proportional_cost(
    ::OptimizationContainer,
    ::PSY.MarketBidCost,
    ::Type{OnVariable},
    comp::Union{PSY.Generator, PSY.ControllableLoad},
    ::Type{<:AbstractDeviceFormulation},
    ::Int,
) = IOM.get_initial_input(_onvar_offer_direction(comp), comp)

# Time-series MarketBidCost: read from parameter container populated by add_parameters!.
function proportional_cost(
    container::OptimizationContainer,
    ::PSY.MarketBidTimeSeriesCost,
    ::Type{OnVariable},
    comp::T,
    ::Type{<:AbstractDeviceFormulation},
    t::Int,
) where {T <: Union{PSY.Generator, PSY.ControllableLoad}}
    param = _cost_at_min_param(_onvar_offer_direction(comp))
    name = get_name(comp)
    param_arr = get_parameter_array(container, param, T)
    param_mult = get_parameter_multiplier_array(container, param, T)
    return param_arr[name, t] * param_mult[name, t]
end

is_time_variant_term(::PSY.MarketBidCost) = false
is_time_variant_term(::PSY.MarketBidTimeSeriesCost) = true

#################################################################################
# Section 2: _consider_parameter — compact commitment startup
# Compact/multi-start formulations have HotStart/WarmStart/ColdStart variables
# in addition to the normal StartVariable.
#################################################################################

_consider_parameter(
    ::Type{StartupCostParameter},
    container::OptimizationContainer,
    ::DeviceModel{T, D},
) where {T, D <: AbstractCompactUnitCommitment} =
    any(has_container_key.([container], [StartVariable, MULTI_START_VARIABLES...], [T]))

#################################################################################
# Section 3: Device-specific validate_occ_component
#################################################################################

# ThermalMultiStart: accept NTuple{3, Float64} and StartUpStages without warning
function IOM.validate_occ_component(
    ::Type{StartupCostParameter},
    device::PSY.ThermalMultiStart,
)
    startup = PSY.get_start_up(PSY.get_operation_cost(device))
    # TupleTimeSeries{StartUpStages} guarantees NTuple{3, Float64} values at construction
    startup isa IS.TupleTimeSeries && return
    _validate_eltype(
        Union{Float64, NTuple{3, Float64}, StartUpStages},
        device,
        startup,
        " startup cost",
    )
end

# Renewable / Storage: warn on nonzero startup, shutdown, and no-load costs

function IOM.validate_occ_component(
    ::Type{StartupCostParameter},
    device::Union{PSY.RenewableDispatch, PSY.Storage},
)
    startup = PSY.get_start_up(PSY.get_operation_cost(device))
    apply_maybe_across_time_series(device, startup) do x
        # x may be Float64 (TGC), StartUpStages (static MBC), or NTuple{3, Float64}
        # (TupleTimeSeries elements). `values` normalizes both NamedTuple and Tuple.
        if any(!iszero, x isa Number ? (x,) : values(x))
            @warn "Nonzero startup cost detected for renewable generation or storage device $(get_name(device))."
        end
    end
end

# LinearCurve (static) and TimeSeriesLinearCurve (TS) are the only types carried in
# MBC/ImportExportCost shutdown and no-load fields. Only the static case is meaningfully
# comparable to zero at validation time — for TS we'd need to iterate the series, which
# the time-series store may not even have populated yet.
# FIXME better solution?
_scalar_if_static(x::IS.LinearCurve) = IS.get_proportional_term(x)
_scalar_if_static(::IS.TimeSeriesLinearCurve) = nothing

function IOM.validate_occ_component(
    ::Type{ShutdownCostParameter},
    device::Union{PSY.RenewableDispatch, PSY.Storage},
)
    x = _scalar_if_static(PSY.get_shut_down(PSY.get_operation_cost(device)))
    if !isnothing(x) && x != 0.0
        @warn "Nonzero shutdown cost detected for renewable generation or storage device $(get_name(device))."
    end
end

function IOM.validate_occ_component(
    ::Type{IncrementalCostAtMinParameter},
    device::Union{PSY.RenewableDispatch, PSY.Storage},
)
    x = _scalar_if_static(PSY.get_no_load_cost(PSY.get_operation_cost(device)))
    if !isnothing(x) && x != 0.0
        @warn "Nonzero no-load cost detected for renewable generation or storage device $(get_name(device))."
    end
end

function IOM.validate_occ_component(
    ::Type{DecrementalCostAtMinParameter},
    device::PSY.Storage,
)
    x = _scalar_if_static(PSY.get_no_load_cost(PSY.get_operation_cost(device)))
    if !isnothing(x) && x != 0.0
        @warn "Nonzero no-load cost detected for storage device $(get_name(device))."
    end
end

#################################################################################
# Section 4: _include_min_gen_power_in_constraint
# Whether the PWL block offer constraint should include p_min * on_var.
#################################################################################

_include_min_gen_power_in_constraint(
    ::Type{<:PSY.Source},
    ::Type{ActivePowerOutVariable},
    ::Type{<:AbstractDeviceFormulation},
) = false
_include_min_gen_power_in_constraint(
    ::Type{<:PSY.Source},
    ::Type{ActivePowerInVariable},
    ::Type{<:AbstractDeviceFormulation},
) = false
_include_min_gen_power_in_constraint(
    ::Type{<:PSY.RenewableDispatch},
    ::Type{ActivePowerVariable},
    ::Type{<:AbstractDeviceFormulation},
) = false
_include_min_gen_power_in_constraint(
    ::Type{<:PSY.Generator},
    ::Type{ActivePowerVariable},
    ::Type{<:AbstractDeviceFormulation},
) = true
_include_min_gen_power_in_constraint(
    ::Type{<:PSY.ControllableLoad},
    ::Type{ActivePowerVariable},
    ::Type{PowerLoadInterruption},
) = true
_include_min_gen_power_in_constraint(
    ::Type{<:PSY.ControllableLoad},
    ::Type{ActivePowerVariable},
    ::Type{PowerLoadDispatch},
) = false
_include_min_gen_power_in_constraint(
    ::Type,
    ::Type{PowerAboveMinimumVariable},
    ::Type{<:AbstractDeviceFormulation},
) = false

#################################################################################
# Section 5: _include_constant_min_gen_power_in_constraint
# Whether the PWL block offer constraint should include p_min as a constant
# (for formulations that have nonzero minimum power but no OnVariable).
#################################################################################

_include_constant_min_gen_power_in_constraint(
    ::Type{<:PSY.ControllableLoad},
    ::Type{ActivePowerVariable},
    ::Type{PowerLoadDispatch},
) = true
_include_constant_min_gen_power_in_constraint(
    ::Type{<:PSY.ControllableLoad},
    ::Type{ActivePowerVariable},
    ::Type{PowerLoadInterruption},
) = false
_include_constant_min_gen_power_in_constraint(
    ::Type{<:PSY.RenewableGen},
    ::Type{ActivePowerVariable},
    ::Type{<:AbstractRenewableDispatchFormulation},
) = true

#################################################################################
# Section 6: Source ImportExport — both incremental and decremental offers
#################################################################################

# FIXME behavior change: we now always add PWL terms for both import and export. The
# previous `isnothing(...)` guard is dead in the new PSY (offer curves default to
# `ZERO_OFFER_CURVE`, not nothing), and we don't yet have a way to introspect TS-backed
# curves to decide "trivially empty". Skipping when the curve is trivial (one-directional
# source) would be the better behavior — revisit once we have a cheap emptiness check.
function add_variable_cost_to_objective!(
    container::OptimizationContainer,
    ::Type{ActivePowerOutVariable},
    component::PSY.Source,
    cost_function::IOM.IEC_TYPES,
    ::Type{ImportExportSourceModel},
)
    isnothing(get_output_offer_curves(cost_function)) && return
    add_pwl_term_delta!(
        IncrementalOffer(),
        container,
        component,
        cost_function,
        ActivePowerOutVariable,
        ImportExportSourceModel,
    )
    return
end

function add_variable_cost_to_objective!(
    container::OptimizationContainer,
    ::Type{ActivePowerInVariable},
    component::PSY.Source,
    cost_function::IOM.IEC_TYPES,
    ::Type{ImportExportSourceModel},
)
    isnothing(get_input_offer_curves(cost_function)) && return
    add_pwl_term_delta!(
        DecrementalOffer(),
        container,
        component,
        cost_function,
        ActivePowerInVariable,
        ImportExportSourceModel,
    )
    return
end

#################################################################################
# Section 7: Load formulation — decremental offers only
#################################################################################

function add_variable_cost_to_objective!(
    container::OptimizationContainer,
    ::Type{T},
    component::PSY.Component,
    cost_function::PSY.OfferCurveCost,
    ::Type{U},
) where {T <: VariableType, U <: AbstractControllablePowerLoadFormulation}
    component_name = PSY.get_name(component)
    @debug "Market Bid" _group = LOG_GROUP_COST_FUNCTIONS component_name
    if IOM.is_nontrivial_offer(get_output_offer_curves(cost_function))
        throw(
            ArgumentError(
                "Component $(component_name) is not allowed to participate as a supply.",
            ),
        )
    end
    add_pwl_term_delta!(
        DecrementalOffer(),
        container,
        component,
        cost_function,
        T,
        U,
    )
    return
end

_vom_offer_direction(::Type{<:AbstractControllablePowerLoadFormulation}) =
    DecrementalOffer()

#################################################################################
# Section 7: Service-specific PWL (ReserveDemandCurve, StepwiseCostReserve)
#################################################################################

"""
PWL block offer constraints for ORDC (ReserveDemandCurve).
"""
function add_pwl_constraint_delta!(
    container::OptimizationContainer,
    component::T,
    ::Type{U},
    break_points::Vector{Float64},
    pwl_vars::Vector{JuMP.VariableRef},
    period::Int,
) where {T <: PSY.ReserveDemandCurve, U <: ServiceRequirementVariable}
    name = PSY.get_name(component)
    variables = get_variable(container, U, T, name)
    const_container = lazy_container_addition!(
        container,
        PiecewiseLinearBlockIncrementalOfferConstraint,
        T,
        axes(variables)...;
        meta = name,
    )
    add_pwl_block_offer_constraints!(
        get_jump_model(container),
        const_container,
        name,
        period,
        variables[name, period],
        pwl_vars,
        break_points,
    )
    return
end

"""
PWL cost terms for StepwiseCostReserve (AbstractServiceFormulation).
"""
function add_pwl_term_delta!(
    container::OptimizationContainer,
    component::T,
    cost_data::PSY.CostCurve{PSY.PiecewiseIncrementalCurve},
    ::Type{U},
    ::Type{V},
) where {T <: PSY.Component, U <: VariableType, V <: AbstractServiceFormulation}
    multiplier = objective_function_multiplier(U, V)
    resolution = get_resolution(container)
    dt = Dates.value(Dates.Second(resolution)) / SECONDS_IN_HOUR
    base_power = get_model_base_power(container)
    value_curve = PSY.get_value_curve(cost_data)
    power_units = PSY.get_power_units(cost_data)
    cost_component = PSY.get_function_data(value_curve)
    device_base_power = PSY.get_base_power(component)
    data = get_piecewise_curve_per_system_unit(
        cost_component,
        power_units,
        base_power,
        device_base_power,
    )
    name = PSY.get_name(component)
    time_steps = get_time_steps(container)
    pwl_cost_expressions = Vector{JuMP.AffExpr}(undef, time_steps[end])
    slopes = IS.get_y_coords(data)
    break_points = PSY.get_x_coords(data)
    for t in time_steps
        pwl_vars = add_pwl_variables_delta!(
            container,
            PiecewiseLinearBlockIncrementalOffer,
            T,
            name,
            t,
            length(slopes);
            upper_bound = Inf,
        )
        add_pwl_constraint_delta!(container, component, U, break_points, pwl_vars, t)
        pwl_cost_expressions[t] =
            get_pwl_cost_expression_delta(pwl_vars, slopes, multiplier * dt)
    end
    return pwl_cost_expressions
end
